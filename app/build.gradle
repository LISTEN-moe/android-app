import org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.apollographql.apollo'

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    flavorDimensions 'variant'

    defaultConfig {
        applicationId 'me.echeung.moemoekyun'
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 87
        versionName '5.1.0'

        vectorDrawables.useSupportLibrary = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            shrinkResources true

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
            pseudoLocalesEnabled false
            zipAlignEnabled true
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'

            minifyEnabled false
        }
    }

    productFlavors {
        playstore {
            dimension 'variant'
        }
        fdroid {
            dimension 'variant'

            applicationIdSuffix '.fdroid'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
    }

    lintOptions {
        disable 'MissingTranslation'
        disable 'ExtraTranslation'

        enable 'ObsoleteSdkInt'

        abortOnError true
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    final coroutines_version = '1.3.7'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    kapt 'com.android.databinding:compiler:3.1.4'

    implementation 'org.koin:koin-android:2.1.5'

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta6'
    implementation 'androidx.fragment:fragment:1.2.4'
    implementation 'androidx.media:media:1.1.0'
    implementation 'androidx.palette:palette:1.0.0'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.preference:preference:1.1.1'
    implementation 'androidx.recyclerview:recyclerview:1.1.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.0.0'

    implementation 'org.greenrobot:eventbus:3.2.0'

    implementation 'com.google.android.material:material:1.1.0'

    implementation 'com.github.tfcporciuncula:flow-preferences:1.1.1'

    implementation 'com.squareup.okhttp3:okhttp:4.7.2'
    implementation 'com.squareup.moshi:moshi:1.8.0'

    implementation "com.apollographql.apollo:apollo-runtime:$apollo_version"
    implementation "com.apollographql.apollo:apollo-http-cache:$apollo_version"
    implementation "com.apollographql.apollo:apollo-coroutines-support:$apollo_version"

    final glide_version = '4.11.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    final exoplayer_version = '2.11.4'
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    implementation "com.google.android.exoplayer:extension-cast:$exoplayer_version"

    // Google Cast support (not available in fdroid flavour)
    playstoreImplementation 'com.google.android.gms:play-services-cast-framework:18.1.0'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'

    // For detecting memory leaks; see https://square.github.io/leakcanary/
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}

apollo {
    generateKotlinModels = true
}

buildscript {
    ext.kotlin_version = '1.3.72'
    ext.apollo_version = '2.0.3'
    repositories {
        mavenCentral()
        jcenter()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.apollographql.apollo:apollo-gradle-plugin:$apollo_version"
    }
}

// See https://kotlinlang.org/docs/reference/experimental.html#experimental-status-of-experimental-api-markers
tasks.withType(AbstractKotlinCompile).all {
    kotlinOptions.freeCompilerArgs += ["-Xopt-in=kotlin.Experimental"]
}

preBuild.dependsOn(ktlintFormat)
