import org.jetbrains.kotlin.gradle.tasks.AbstractKotlinCompile

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlinx-serialization'
apply plugin: 'com.apollographql.apollo3'

android {
    compileSdkVersion 31

    defaultConfig {
        applicationId 'me.echeung.moemoekyun'
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 97
        versionName '5.4.1'
    }

    buildFeatures {
        dataBinding = true
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions 'variant'

    productFlavors {
        playstore {
            dimension 'variant'
        }
        fdroid {
            dimension 'variant'

            applicationIdSuffix '.fdroid'
        }
    }

    lint {
        abortOnError true
        disable 'MissingTranslation', 'ExtraTranslation'
        enable 'ObsoleteSdkInt'
    }

    packagingOptions {
        resources {
            excludes += [
                    'META-INF/DEPENDENCIES',
                    'LICENSE.txt',
                    'META-INF/LICENSE',
                    'META-INF/LICENSE.txt',
                    'META-INF/NOTICE'
            ]
        }
    }

    dependenciesInfo {
        includeInApk = false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

dependencies {
    final coroutines_version = '1.6.0'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation 'io.insert-koin:koin-android:3.1.4'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'
    implementation 'androidx.fragment:fragment-ktx:1.4.1'
    implementation 'androidx.media:media:1.4.3'
    implementation 'androidx.palette:palette-ktx:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.2.0-alpha01'

    implementation 'com.google.android.material:material:1.5.0'

    implementation 'com.github.tfcporciuncula:flow-preferences:1.3.4'

    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2'

    implementation "com.apollographql.apollo3:apollo-runtime:$apollo_version"
    implementation "com.apollographql.apollo3:apollo-http-cache:$apollo_version"

    final glide_version = '4.12.0'
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    final exoplayer_version = '2.16.1'
    implementation "com.google.android.exoplayer:exoplayer-core:$exoplayer_version"
    playstoreImplementation "com.google.android.exoplayer:extension-cast:$exoplayer_version"

    // Google Cast support (not available in fdroid flavour)
    playstoreImplementation 'com.google.android.gms:play-services-cast-framework:21.0.1'

    implementation 'de.psdev.licensesdialog:licensesdialog:2.1.0'

    implementation 'com.squareup.logcat:logcat:0.1'

    // For detecting memory leaks; see https://square.github.io/leakcanary/
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.2'
}

apollo {
    useVersion2Compat()
}

buildscript {
    ext.kotlin_version = '1.6.10'
    ext.apollo_version = '3.0.0'
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.apollographql.apollo3:apollo-gradle-plugin:$apollo_version"
    }
}

// See https://kotlinlang.org/docs/reference/experimental.html#experimental-status-of-experimental-api-markers
tasks.withType(AbstractKotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            "-Xopt-in=kotlin.Experimental",
            "-Xopt-in=kotlin.RequiresOptIn",
            "-Xopt-in=kotlinx.coroutines.ExperimentalCoroutinesApi",
            "-Xopt-in=kotlinx.coroutines.FlowPreview",
    ]
}

preBuild.dependsOn(formatKotlin)
